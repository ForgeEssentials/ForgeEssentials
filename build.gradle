// Build repositories
buildscript {
	repositories {
		maven { url = 'https://maven.minecraftforge.net' }
		maven {
			name = "sonatype releases"
			url = "https://oss.sonatype.org/content/repositories/releases/"
		}
		maven {
			name = "sonatype snapshots"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
        maven {
            name = "sponge"
            url = "https://repo.spongepowered.org/repository/maven-public/"
        }
		mavenCentral()
		mavenLocal()
	}
	dependencies {
		classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1"
		classpath "org.ajoberstar:gradle-git:0.12.0"
		classpath "com.discord4j:discord4j-core:3.2.1"
 		classpath files('lib/plugins/mixingradle-0.7.39.jar')//built from https://github.com/CreativeMD/MixinGradle/tree/task_fix
	}
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'com.diffplug.eclipse.apt' version '3.35.0'
    id 'org.sonarqube' version '2.5'
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}
apply plugin: 'jacoco'
apply plugin: 'org.spongepowered.mixin'

sonarqube {
    properties {
        properties['sonar.projectKey'] = "com.forgeessentials:forgeessentials"
        properties['sonar.projectName'] = project.mod_name
        properties['sonar.projectVersion'] = project.mod_version
    }
}

// Set up FE required repositories
repositories {
    flatDir {
        dirs 'lib'
    }

	maven {
		name = "CurseForge"
		url = "https://minecraft.curseforge.com/api/maven/"
	}

	maven {
		name = "sk89q"
		url = "https://maven.enginehub.org/repo/"
	}

	maven {
		name = "sponge"
		url = "https://repo.spongepowered.org/maven/"
	}

    maven {
        name 'JEI'
        url "https://dvs1.progwml6.com/files/maven"
        content {
            includeGroup 'mezz.jei'
        }
		allowInsecureProtocol = true
    }

    maven {
        name = "forge"
        url = "https://files.minecraftforge.net/maven"
    }

	maven {
		name "Sonatype (Cubic Chunks)"
		url "https://oss.sonatype.org/content/repositories/public/"
	}
}
/************************************************************
 * General configuration
 */

// Get buildNumber from environment variable
ext.buildNumber = "0"
if (System.getenv("BUILD_NUMBER") != null) {
	buildNumber = System.getenv("BUILD_NUMBER")

}

ext.set 'changelog', System.getenv("CHANGE_LOG")
ext.set 'releaseType', 'alpha'

// Get commit hash

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens
import org.ajoberstar.grgit.Grgit
def repo = Grgit.open(project.file("."))
ext.gitCommitHash = repo.log().find().abbreviatedId

// Set build properties
version = "$mod_version.$buildNumber"
group = "com.forgeessentials"
archivesBaseName = "forgeessentials"
sourceCompatibility = 1.8
targetCompatibility = 1.8
//java.toolchain.languageVersion = JavaLanguageVersion.of(8) // Mojang ships Java 8 to end users, so your mod should target Java 8.
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
println('Running Tasks: ' +project.gradle.startParameter.taskNames)

ext.set 'srcFile',file("./tmp/$archivesBaseName-$minecraftVersion-$mod_version.$buildNumber-server.jar")

curseforge {
    apiKey = System.getenv("CF_APIKEY")
    if (apiKey) {
        project {
            id = projectID
            changelog = project.changelog
            releaseType = project.releaseType
            addGameVersion minecraftVersion
            addGameVersion 'Forge'
            addGameVersion 'Java 8'

            mainArtifact(project.srcFile) {
                displayName = "$archivesBaseName-$minecraftVersion-$mod_version.$buildNumber"
                relations {
                    optionalDependency 'baubles'
                }
            }
        }
    }
    options {
        javaVersionAutoDetect = false
        forgeGradleIntegration = false
        javaIntegration = false
    }
}
// Set up forge options
minecraft {
	mappings channel: 'official', version: '1.16.5'
    //accessTransformer = file('src/main/resources/META-INF/forgeessentials_at.cfg')
    runs {
	    client {
	            workingDirectory project.file('rundir')

	            // Recommended logging data for a userdev environment
	            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

	            // Recommended logging level for the console
	            property 'forge.logging.console.level', 'debug'

	            mods {
	            }
	        }
        server {
            workingDirectory project.file('rundir')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
            }
        }
    }
}

// Configure shared manifest
ext.sharedManifest = manifest {
	attributes (
		"BuildID": gitCommitHash,
		"BuildNumber": buildNumber,
		"FMLCorePluginContainsFMLMod": "true",
		"ForceLoadAsMod": "true",
        "Specification-Title": "forgeessentials",
        "Specification-Vendor": "forgeessentials",
        "Specification-Version": "1", // We are version 1 of ourselves
        "Implementation-Title": project.name,
        "Implementation-Version": "$mod_version",
        "Implementation-Vendor" :"forgeessentials",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
	)
}

ext.serverManifest = manifest {
	from sharedManifest
	attributes (
		//"TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
		"MixinConfigs": "mixins.forgeessentials.json",
		//"FMLAT": "forgeessentials_at.cfg",
	)
}

ext.clientManifest = manifest {
	from sharedManifest
	attributes (
		"MixinConfigs": "mixins.forgeessentials.client.json"
	)
}

/************************************************************
 * Dependency configuration
 */

// Create dependency configurations
configurations {
	clientannotationProcessor
	serverannotationProcessor
	shadowLib
	shadowClientLib
	compile.extendsFrom buildDep, shadowLib, shadowClientLib//, serverLib, shade, shadeClient
}

// def jdkHome = System.getenv("JAVA_HOME")

// Configure dependencies (build-only, shaded and packed libraries)
dependencies {

    minecraft "net.minecraftforge:forge:$project.minecraftVersion-$project.forgeVersion"
	 // IRC
	implementation "org.pircbotx:pircbotx:2.1"
	shadowLib("org.pircbotx:pircbotx:2.1") {
        // these are all packaged with the minecraft server jar
        exclude module: 'guava'
        exclude module: 'commons-lang3'
        exclude module: 'commons-codec'
    }

	implementation 'net.dv8tion:JDA:5.0.0-beta.8'
	shadowLib('net.dv8tion:JDA:5.0.0-beta.8')

	// Database libraries
	shadowLib group: 'com.h2database', 	name: 'h2', 					version: '1.3.175'
	shadowLib group: 'mysql', 			name: 'mysql-connector-java', 	version: '5.1.22'
	//org.hibernate
	implementation 'org.hibernate:hibernate-core:5.6.15.Final'
	implementation 'javax.persistence:javax.persistence-api:2.2'
	shadowLib group: 'org.hibernate', 	  name: 'hibernate-core', 		version: '5.6.15.Final'
	//shadowLib group: 'javax.persistence', name: 'javax.persistence-api', version: '2.2'
	compileOnly   group: 'org.hibernate', 	  name: 'hibernate-jpamodelgen',version: '5.6.15.Final'

	//annonations processor
	serverannotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
	serverannotationProcessor 'org.hibernate:hibernate-jpamodelgen:5.6.15.Final'
	clientannotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
	
	// WorldEdit
	implementation 'com.sk89q.worldedit:worldedit-core:7.2.6-SNAPSHOT'
	implementation 'com.sk89q.worldedit:worldedit-forge-mc1.16.3:7.2.6-SNAPSHOT'

	implementation "baubles:baubles:1.8.7.1:1.16.5"

	implementation 'org.spongepowered:spongeapi:8.0.0'
	compileOnly ("io.github.opencubicchunks:cubicchunks-api:1.12.2-0.0-SNAPSHOT") {
		exclude group: "org.spongepowered", module: "mixin"
	}
	//compileOnly "com.feed_the_beast.mods:FTBLib:5.4.7.2"
	//compileOnly "com.feed_the_beast.mods:FTBUtilities:5.4.1.131"

	// TOOLS.jar from jdk for javadoc support
	implementation files(System.getenv("JAVA_HOME") + "/lib/tools.jar")

	//jGit library
	implementation 'org.eclipse.jgit:org.eclipse.jgit:5.13.0.202109080827-r'
	shadowLib group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.13.0.202109080827-r'
	shadowClientLib group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.13.0.202109080827-r'
}

/************************************************************
 * Configure source sets (e.g., src/main)
 */

sourceSets {
    def deps = [
		configurations.minecraft,
		configurations.compile,
		configurations.compileClasspath,
		configurations.runtimeClasspath
	]
	utils {
		compileClasspath = files(deps)
	}
	main {
		compileClasspath += files(sourceSets.utils.compileClasspath, sourceSets.utils.output)
        ext.refMap = "mixins.forgeessentials.refmap.json"
	}
	client {
		compileClasspath += files(sourceSets.main.compileClasspath)
        ext.refMap = "mixins.forgeessentials.client.refmap.json"
	}
	utils {
		if(!project.gradle.startParameter.taskNames.contains("build")) {
			compileClasspath = files(sourceSets.main.compileClasspath)
		}
	}
}

// Configure utils tokens
task processSource(type: Sync) {
    from sourceSets.utils.java
    filter(ReplaceTokens, tokens: ['_MCVERSION_':project.minecraftVersion, '_BASEVERSION_': project.mod_base, '_MAJORVERSION_': project.mod_major])
    into "$buildDir/src"
}

project.compileUtilsJava {
	source = processSource.outputs
} 

// Configure server resource processing
project.processResources {
	from (sourceSets.main.resources.srcDirs) {
		include 'META-INF/mods.toml'
		duplicatesStrategy = DuplicatesStrategy.INCLUDE
		expand (
			'version': "$mod_version",
			'mod_name': mod_name,
			'mod_download': mod_download,
			'buildHash': gitCommitHash,
			'buildNumber': buildNumber,
		)
	}
}

// Configure client resource processing
project.processClientResources {
	from (sourceSets.client.resources.srcDirs) {
		include 'META-INF/mods.toml'
		duplicatesStrategy = DuplicatesStrategy.INCLUDE
		expand (
			'version': "$mod_version",
			'mod_name': mod_name_client
		)
	}
}

mixin {
    add sourceSets.main, "mixins.forgeessentials.refmap.json"
    add sourceSets.client, "mixins.forgeessentials.client.refmap.json"

    config 'mixins.forgeessentials.json'
    config 'mixins.forgeessentials.client.json'
    disableTargetValidator = false
    overwriteErrorLevel = 'error'
}

compileJava {
    options.annotationProcessorPath = configurations.serverannotationProcessor
}
compileClientJava {
    options.annotationProcessorPath = configurations.clientannotationProcessor
}
/************************************************************
 * Configure tsd jar
 */

 // JavaExec task for generating tsd
//  Calls the main class for the TSD generator since that is working
/*
task tsd(type: JavaExec) {
    dependsOn "classes"
    classpath = project.sourceSets.test.compileClasspath
    mainClass = "com.forgeessentials.jscripting.TsdGenerator"
    doLast {
        copy {
            from files("jscripting/") {
                include "*.d.ts"
            }
			duplicatesStrategy = DuplicatesStrategy.INCLUDE
            into "build/resources/main/com/forgeessentials/jscripting"
        }
    }
}*/

/************************************************************
 * Configure client jar
 */

task clientshadowJar(type: ShadowJar) {
	minimize()
    description = "ShadowJar for client code"
    archiveClassifier = 'shadow'
    from sourceSets.client.output
    from sourceSets.utils.output
    configurations = [project.configurations.shadowClientLib]
}

task clientJar (dependsOn: ["clientClasses", "clientshadowJar"], type: Jar) {
	archiveClassifier = "client"
	archiveAppendix = project.minecraftVersion
	
	from sourceSets.client.output
	from sourceSets.utils.output
	duplicatesStrategy = DuplicatesStrategy.INCLUDE

	from (zipTree(clientshadowJar.archiveFile)) {
		exclude 'META-INF','META-INF/**'
		exclude 'assets/**'
		exclude 'about.html'
		exclude 'plugin.properties'
	}
	exclude 'mixins.forgeessentials.refmap.json'
	manifest {
		from clientManifest
	}
}
clientJar.doLast {
	delete (clientshadowJar.archiveFile)
}
/************************************************************
 * Configure shadow jar
 */

shadowJar {
	dependsOn clientJar
	minimize {
 	   exclude(dependency('org.hibernate:.*:.*'))
 	   exclude(dependency('mysql:.*:.*'))
 	   exclude(dependency('com.h2database:.*:.*'))
 	   exclude(dependency('javax:.*:.*'))
 	}
	//relocate  'org.dom4j','com.forgeessentials.thirdparty.org.dom4j'
	relocate  'org.eclipse.jgit','com.forgeessentials.thirdparty.org.eclipse.jgit'
	relocate  'org.gjt','com.forgeessentials.thirdparty.org.gjt'
	relocate  'org.hibernate','com.forgeessentials.thirdparty.org.hibernate'
	relocate  'org.h2','com.forgeessentials.thirdparty.org.h2'
	relocate  'org.jboss','com.forgeessentials.thirdparty.org.jboss'
	relocate  'org.pircbotx','com.forgeessentials.thirdparty.org.pircbotx'
	relocate  'org.slf4j','com.forgeessentials.thirdparty.org.slf4j'
	relocate  'javax.persistence','com.forgeessentials.thirdparty.javax.persistence'
	relocate  'javax.transaction','com.forgeessentials.thirdparty.javax.transaction'
	//relocate  'javax.interceptor','com.forgeessentials.thirdparty.javax.interceptor'
	//relocate  'javax.inject','com.forgeessentials.thirdparty.javax.inject'
	//relocate  'javax.enterprise','com.forgeessentials.thirdparty.javax.enterprise'
	//relocate  'javax.el','com.forgeessentials.thirdparty.javax.el'
	//relocate  'javax.decorator','com.forgeessentials.thirdparty.javax.decorator'
	//relocate  'javax.annotation','com.forgeessentials.thirdparty.javax.annotation'
	relocate  'javax.activation','com.forgeessentials.thirdparty.javax.activation'
	//relocate  'javax.xml','com.forgeessentials.thirdparty.javax.xml'
	//relocate  'javassist','com.forgeessentials.thirdparty.javassist'
	relocate  'antlr','com.forgeessentials.thirdparty.antlr'
	//relocate  'com.fasterxml','com.forgeessentials.thirdparty.com.fasterxml'
	relocate  'com.mysql','com.forgeessentials.thirdparty.com.mysql'
	configurations = [project.configurations.shadowLib]
	archiveClassifier = "shadow"

	from sourceSets.main.output
	from sourceSets.utils.output
	exclude('com/forgeessentials/client/**')
}

/************************************************************
 * Configure server jar (default)
 */
 
jar {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
	dependsOn clientJar
	dependsOn shadowJar
    //dependsOn "tsd"
    archiveClassifier = "server"
	archiveAppendix = project.minecraftVersion
	
	from sourceSets.main.output
	from sourceSets.utils.output
    exclude('com/forgeessentials/client/**')

	from (zipTree(shadowJar.archivePath)) {
		exclude 'META-INF','META-INF/**'
		exclude 'assets/**'
		exclude 'natives','natives/**'
		exclude '.gitkeep'
		exclude 'about.html'
		exclude 'plugin.properties'
	}

	from (zipTree(shadowJar.archivePath)) {
		include 'META-INF/services/*'
		//include 'META-INF/persistence.xml'
		//filter {
		//	line -> line.replaceAll('org.hibernate','com.forgeessentials.thirdparty.org.hibernate')
		//}
		//filter {
		//	line -> line.replaceAll('org.h2','com.forgeessentials.thirdparty.org.h2')
		//}
		//rename {
		//	name -> "com.forgeessentials.thirdparty.$name"
		//}
	}

	exclude('mixins.forgeessentials.client.refmap.json')
	exclude('mapper_colorscheme.txt')
	manifest {
		from serverManifest
	}
}
jar.doLast {
	delete (shadowJar.archivePath)
	delete (processSource.outputs)
}

/************************************************************
 * Deobfuscated server jar
 * Dependency name for maven = com.forgeessentials:forgeessentials:<version>:
 * */

task deobfJar(dependsOn: "classes", type: Jar) {
	archiveClassifier = "dev-"+gitCommitHash
	archiveAppendix = project.minecraftVersion
	
	from sourceSets.main.output
	from sourceSets.utils.output
	
	from (sourceSets.main.java) {
		into ("src/")
	}

	manifest {
		from serverManifest
	}
}
//
//task sourceJar(type: Jar) {
//	classifier = "sources"
//	appendix = project.minecraftVersion
//
//	from sourceSets.main.allSource
//
//	manifest {
//		from serverManifest
//	}
//}


/************************************************************
 * Configure API javadoc
 */
/*
javadoc {
	include "com/forgeessentials/api/**"
	classpath = sourceSets.main.compileClasspath + sourceSets.main.output
	options.addBooleanOption("Xdoclint:reference,accessibility,html", true) // excluded: syntax, missing
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = "javadoc"
	appendix = project.minecraftVersion
	
	from javadoc.destinationDir
	
	manifest { from sharedManifest }
}*/

/************************************************************
 * Configure Annotations for IDEs Jar
 */
task annotations(type: Jar) {
	archiveClassifier = "annonations"
	archiveAppendix = project.minecraftVersion
	
    from(sourceSets.main.output) {
        include "com/forgeessentials/playerlogger/entity/**"
        exclude "com/forgeessentials/playerlogger/entity/Action.class"
        exclude "com/forgeessentials/playerlogger/entity/Action01Block\$ActionBlockType.class"
        exclude "com/forgeessentials/playerlogger/entity/Action01Block.class"
        exclude "com/forgeessentials/playerlogger/entity/Action02Command.class"
        exclude "com/forgeessentials/playerlogger/entity/Action03PlayerEvent\$PlayerEventType.class"
        exclude "com/forgeessentials/playerlogger/entity/Action03PlayerEvent.class"
        exclude "com/forgeessentials/playerlogger/entity/Action04PlayerPosition.class"
        exclude "com/forgeessentials/playerlogger/entity/BlockData.class"
        exclude "com/forgeessentials/playerlogger/entity/PlayerData.class"
        exclude "com/forgeessentials/playerlogger/entity/WorldData.class"
    }  
}
task copyJarToGenerated(type: Copy) {
    from annotations
    into "misc/"
}
annotations.finalizedBy copyJarToGenerated

/************************************************************
 * Others
 */

reobf {
	jar{}
    clientJar{}
}

 // Add default artifacts for task "build"
artifacts {
	archives deobfJar
	archives clientJar
	archives annotations
}
