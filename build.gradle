// Build repositories
buildscript {
	repositories {
		maven {
			name = "forge"
			url = "https://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/releases/"
		}
        maven {
            name = "sponge"
            url = "https://repo.spongepowered.org/maven/"
        }
        maven {
      		url = uri("https://plugins.gradle.org/m2/")
    	}
		mavenCentral()
		mavenLocal()
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
		jcenter()

	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:5.1.68"
		classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1"
		classpath "org.ajoberstar:gradle-git:0.12.0"
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
		classpath "com.discord4j:discord4j-core:3.2.1"
		classpath("com.diffplug.gradle:goomph:3.42.2")
	}
}

plugins {
    id 'org.sonarqube' version '2.5'
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "2.+"
}
apply plugin: 'com.diffplug.eclipse.apt'
apply plugin: 'jacoco'

sonarqube {
    properties {
        properties['sonar.projectKey'] = "com.forgeessentials:forgeessentials"
        properties['sonar.projectName'] = project.name
        properties['sonar.projectVersion'] = project.version
    }
}

//Clear repositories since spongepowered plugin is inserting an extra repo with http!
//TODO: Update to a newer version of the spongepowered plugin
repositories.clear()
// Set up FE required repositories
repositories {
    flatDir {
        dirs 'lib'
    }

	maven {
		name = "CurseForge"
		url = "https://minecraft.curseforge.com/api/maven/"
	}

	maven {
		name = "sk89q"
		url = "https://maven.enginehub.org/repo/"
	}

	maven {
		name = "sponge"
		url = "https://repo.spongepowered.org/maven/"
	}

    maven {
        name = "JEI"
        url "http://dvs1.progwml6.com/files/maven"
		allowInsecureProtocol = true
    }

    maven {
        name = "forge"
        url = "https://files.minecraftforge.net/maven"
    }

	maven {
		name "Sonatype (Cubic Chunks)"
		url "https://oss.sonatype.org/content/repositories/public/"
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'


import org.ajoberstar.grgit.Grgit
import org.apache.tools.ant.filters.ReplaceTokens

/************************************************************
 * General configuration
 */

// Get buildNumber from environment variable
ext.buildNumber = "0"
if (System.getenv("BUILD_NUMBER") != null) {
	buildNumber = System.getenv("BUILD_NUMBER")

}

ext.set 'changelog', System.getenv("CHANGE_LOG")
ext.set 'releaseType', 'release'

// Get commit hash

def repo = Grgit.open(project.file("."))
ext.gitCommitHash = repo.log().find().abbreviatedId

// Set build properties
version = "$mod_base.$mod_major.$buildNumber"
group = "com.forgeessentials"
archivesBaseName = "forgeessentials"
sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.set 'srcFile',file("./tmp/$archivesBaseName-$minecraftVersion-$mod_base.$mod_major.$buildNumber-server.jar")
ext.set 'clientSrcFile',file("./tmp/$archivesBaseName-$minecraftVersion-$mod_base.$mod_major.$buildNumber-client.jar")

curseforge {
    apiKey = System.getenv("CF_APIKEY")
    if (apiKey) {
        project {
            id = projectID
            changelog = project.changelog
            releaseType = project.releaseType
            addGameVersion minecraftVersion
            addGameVersion 'Forge'
            addGameVersion 'Java 8'

            mainArtifact(project.srcFile) {
                displayName = "$archivesBaseName-$minecraftVersion-$mod_base.$mod_major.$buildNumber"
                relations {
                    optionalDependency 'baubles'
                }
            }
        }
        project {
            id = clientProjectID
            changelog = project.changelog
            releaseType = project.releaseType
            addGameVersion minecraftVersion
            addGameVersion 'Forge'
            addGameVersion 'Java 8'

            mainArtifact(project.clientSrcFile) {
                displayName = "$archivesBaseName-$minecraftVersion-$mod_base.$mod_major.$buildNumber"
                relations {
                    tool('forge-essentials')
                }
            }
        }
    }
    options {
        javaVersionAutoDetect = false
        forgeGradleIntegration = false
        javaIntegration = false
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "forge-essentials"
    versionNumber = "$minecraftVersion-$mod_base.$mod_major.$buildNumber"
    versionType = project.releaseType
    versionName = "$archivesBaseName-$minecraftVersion-$mod_base.$mod_major.$buildNumber"
    uploadFile = project.srcFile
    gameVersions = [minecraftVersion]
    loaders = ["forge"]
    changelog = project.changelog
}

project.afterEvaluate {
    var task = tasks.findByPath("modrinth")
    task.dependsOn = []
    task = tasks.findByPath("curseforge")
    task.dependsOn = []
}

// Set up forge options
minecraft {
    mappings channel: 'snapshot', version: "20180304-1.12"
    accessTransformer = file('src/main/resources/META-INF/forgeessentials_at.cfg')
    runs {
        client {
            workingDirectory project.file('rundir')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
            }
        }

        server {
            workingDirectory project.file('rundir')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
            }
        }
    }
}

// Configure shared manifest
ext.sharedManifest = manifest {
	attributes (
		"BuildID": gitCommitHash,
		"BuildNumber": buildNumber,
		"FMLCorePluginContainsFMLMod": "true",
		"ForceLoadAsMod": "true",
        "MixinCompatibilityLevel": "JAVA_8",
	)
}

ext.serverManifest = manifest {
	from sharedManifest
	attributes (
		"TweakClass": "com.forgeessentials.core.preloader.FELaunchHandler",
		"TweakOrder": "0",
		"MixinConfigs": "mixins.forgeessentials.json",
		"FMLAT": "forgeessentials_at.cfg",
	)
}

/************************************************************
 * Dependency configuration
 */

// Create dependency configurations
configurations {
	includeMod
	shade
	shadeClient
	serverLib
	compile.extendsFrom shade, shadeClient, serverLib
    mavenDeps
}

// def jdkHome = System.getenv("JAVA_HOME")

// Configure dependencies (build-only, shaded and packed libraries)
dependencies {

    minecraft "net.minecraftforge:forge:$project.minecraftVersion-$project.forgeVersion"
	// IRC
	implementation "org.pircbotx:pircbotx:2.0.1"
	serverLib("org.pircbotx:pircbotx:2.0.1") {
        // these are all packaged with the minecraft server jar
        exclude module: 'guava'
        exclude module: 'commons-lang3'
        exclude module: 'commons-codec'
    }

	implementation 'net.dv8tion:JDA:5.0.0-beta.8'
    serverLib('net.dv8tion:JDA:5.0.0-beta.8')
    // Shade Mixin library
    implementation (group: "org.spongepowered", name: "mixin", version: "0.8.1-SNAPSHOT")
    shade (group: "org.spongepowered", name: "mixin", version: "0.8.1-SNAPSHOT") {
        exclude group: 'org.ow2.asm', module: 'asm-tree'
    }
    shadeClient (group: "org.spongepowered", name: "mixin", version: "0.8.1-SNAPSHOT") {
        exclude group: 'org.ow2.asm', module: 'asm-tree'
    }

	// Database libraries	
	serverLib group: 'com.h2database', 	name: 'h2', 					version: '1.3.175'
	serverLib group: 'mysql', 			name: 'mysql-connector-java', 	version: '5.1.22'
	serverLib group: 'org.hibernate', 	name: 'hibernate-core', 		version: '5.2.2.Final'
	implementation 'javax.persistence:javax.persistence-api:2.2'
    annotationProcessor group: 'org.hibernate', 	name: 'hibernate-jpamodelgen', 	version: '5.6.14.Final'
    annotationProcessor 'org.spongepowered:mixin:0.8.5-SNAPSHOT:processor'
	/* serverLib "org.eclipse.persistence:eclipselink:2.6.0" */
	
	// WorldEdit
    implementation "com.sk89q.worldedit:worldedit-core:6.1.4-SNAPSHOT"
    implementation "com.sk89q.worldedit:worldedit-forge-mc1.10.2:6.1.5"
    implementation "baubles:Baubles:1.12:1.5.2"
    //deobfProvided files('lib/worldedit-forge-mc1.10.2-6.1.4-dist.jar')
    mavenDeps "com.sk89q.worldedit:worldedit-core:6.1.4-SNAPSHOT"

	//buildDep "org.spongepowered:spongeforge:3.1.0-BETA-1046:deobf"
	compileOnly 'org.spongepowered:spongeapi:6.0.0'
    compileOnly ("io.github.opencubicchunks:cubicchunks-api:1.12.2-0.0-SNAPSHOT") {
        exclude group: "org.spongepowered", module: "mixin"
    }
    compileOnly "com.feed_the_beast.mods:FTBLib:5.4.7.2"
    compileOnly "com.feed_the_beast.mods:FTBUtilities:5.4.1.131"

	// TOOLS.jar from jdk for javadoc support
    // compile files("$jdkHome/lib/tools.jar")
    implementation files(System.getenv("JAVA_HOME") + "/lib/tools.jar")

	compile 'javax.xml.bind:jaxb-api:2.3.1'
}

/************************************************************
 * Configure source sets (e.g., src/main)
 */

sourceSets {
    def deps = [
		configurations.minecraft,
        configurations.compile,
        configurations.compileClasspath,
        configurations.runtimeClasspath
	]
	utils {
		compileClasspath = files(deps)
	}
	main {
		compileClasspath += files(sourceSets.utils.compileClasspath, sourceSets.utils.output)
        refMap = "mixins.forgeessentials.refmap.json"
	}
	client {
		compileClasspath += files(sourceSets.main.compileClasspath)
        refMap = "mixins.forgeessentials.client.refmap.json"
	}

    test {
        compileClasspath = files(configurations.compile, sourceSets.utils.output, sourceSets.main.output)
        compileClasspath += files deps
    }
}

// Configure utils tokens
task processSource(type: Sync) {
    from sourceSets.utils.java
	filter(ReplaceTokens, tokens: ['_MCVERSION_':project.minecraftVersion, '_BASEVERSION_': project.mod_base, '_MAJORVERSION_': project.mod_major])
    into "$buildDir/src"
}

project.compileUtilsJava {
	source = processSource.outputs
}

// Configure server resource processing
project.processResources {
	from (sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		duplicatesStrategy = DuplicatesStrategy.INCLUDE
		expand (
			"version": project.version, 
			"mcversion": project.minecraftVersion,
		)
	}
}

// Configure client resource processing
project.processClientResources {
	from (sourceSets.client.resources.srcDirs) {
		include "mcmod.info"
		duplicatesStrategy = DuplicatesStrategy.INCLUDE
		expand (
			"version": project.version, 
			"mcversion": project.minecraftVersion,
		)
	}
}

/************************************************************
 * Configure server jar (default)
 */

 // JavaExec task for generating tsd
//  Calls the main class for the TSD generator since that is working

task tsd(type: JavaExec) {
    dependsOn "classes"
    classpath = project.sourceSets.test.compileClasspath
    mainClass = "com.forgeessentials.jscripting.TsdGenerator"
    doLast {
        copy {
            from files("jscripting/") {
                include "*.d.ts"
            }
			duplicatesStrategy = DuplicatesStrategy.INCLUDE
            into "build/resources/main/com/forgeessentials/jscripting"
        }
    }
}

// Pack libraries as zip file to include them in the jar file
task serverLibArchive(type: Zip) {
	archiveName = "libraries.zip"
	destinationDir = jar.destinationDir

	from (configurations.serverLib.copyRecursive()) { into("ForgeEssentials/lib") }
	from (configurations.includeMod) { into("mods") }
}

jar.doLast {
	delete (serverLibArchive.archivePath)
	delete (processSource.outputs)
}

// Configure server jar (default)
jar {
	dependsOn "serverLibArchive"
    dependsOn "tsd"
	classifier = "server"
	appendix = project.minecraftVersion
	
	from sourceSets.main.output
	from sourceSets.utils.output
    exclude('com/forgeessentials/client/**')
    from fileTree(serverLibArchive.archivePath)
	configurations.shade.copyRecursive().setTransitive(false).each { artifact ->
		from (zipTree(artifact))

        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.RSA'
        exclude 'META-INF/*.MF'
        exclude 'LICENSE.txt'
	}

	manifest {
		from serverManifest
	}
}

/************************************************************
 * Configure client jar
 */

task clientJar (dependsOn: "clientClasses", type: Jar) {
	classifier = "client"
	appendix = project.minecraftVersion
	
	from sourceSets.client.output
	from sourceSets.utils.output
	configurations.shadeClient.copyRecursive().setTransitive(false).each { artifact ->
		from (zipTree(artifact))

        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.RSA'
        exclude 'META-INF/*.MF'
        exclude 'LICENSE.txt'
	}
	
	manifest {
		from sharedManifest
		attributes (
			"MixinConfigs": "mixins.forgeessentials.client.json",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
		)
	}
}

/************************************************************
 * Deobfuscated server jar
 * Dependency name for maven = com.forgeessentials:forgeessentials:<version>:
 * */

task deobfJar(dependsOn: "classes", type: Jar) {
	classifier = "dev"
	appendix = project.minecraftVersion
	
	from sourceSets.main.output
	from sourceSets.utils.output
	
	from (sourceSets.main.java) {
		into ("src/")
	}

	manifest {
		from serverManifest
	}
}
//
//task sourceJar(type: Jar) {
//	classifier = "sources"
//	appendix = project.minecraftVersion
//
//	from sourceSets.main.allSource
//
//	manifest {
//		from serverManifest
//	}
//}


/************************************************************
 * Configure API javadoc
 */

javadoc {
	include "com/forgeessentials/api/**"
	classpath = sourceSets.main.compileClasspath + sourceSets.main.output
	options.addBooleanOption("Xdoclint:reference,accessibility,html", true) // excluded: syntax, missing
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = "javadoc"
	appendix = project.minecraftVersion
	
	from javadoc.destinationDir
	
	manifest { from sharedManifest }
}

//reobf {
//    clientJar {
//        classpath = sourceSets.main.compileClasspath
//    }
//}

/************************************************************
 * Configure Annotations for IDEs Jar
 */

// util.jar
task Annotations(type: Jar) {
	classifier = "annotations"
	appendix = project.minecraftVersion
	
    from(sourceSets.main.output) {
        include "com/forgeessentials/playerlogger/entity/**"
        exclude "com/forgeessentials/playerlogger/entity/Action.class"
        exclude "com/forgeessentials/playerlogger/entity/Action01Block\$ActionBlockType.class"
        exclude "com/forgeessentials/playerlogger/entity/Action01Block.class"
        exclude "com/forgeessentials/playerlogger/entity/Action02Command.class"
        exclude "com/forgeessentials/playerlogger/entity/Action03PlayerEvent\$PlayerEventType.class"
        exclude "com/forgeessentials/playerlogger/entity/Action03PlayerEvent.class"
        exclude "com/forgeessentials/playerlogger/entity/Action04PlayerPosition.class"
        exclude "com/forgeessentials/playerlogger/entity/BlockData.class"
        exclude "com/forgeessentials/playerlogger/entity/PlayerData.class"
        exclude "com/forgeessentials/playerlogger/entity/WorldData.class"
        
        doLast {
        delete fileTree(dir:'misc', include: '**annotations.jar')
        copy {
            from files("build/libs/") {
                include "**annotations.jar"
            }
            into "misc"
            rename 'forgeessentials-'+project.minecraftVersion+"-"+project.version+'-annotations.jar', 'forgeessentials-'+project.minecraftVersion+'-annotations.jar'
        }
        delete fileTree(dir:'build/libs/', include: '**annotations.jar')
        }
    }  
}

/************************************************************
 * Others
 */

// Add default artifacts for task "build"
artifacts {
	//archives deobfJar
	archives clientJar
	archives Annotations
}

/************************************************************
 * Configure maven publishing
 */

apply plugin: "maven-publish"

publishing {
	//tasks.publish.dependsOn "deobfJar"

	publications {
		mavenJava (MavenPublication) {
			groupId "com.forgeessentials"
			artifactId "forgeessentials"
			version mod_base + "." + mod_major + "-SNAPSHOT"
			if (System.getenv("PUBLISH_RELEASE"))
				version mod_base + "." + mod_major

			artifact deobfJar
			//artifact sourceJar
			artifact javadocJar

			pom.withXml {
				def dependenciesNode = asNode().appendNode("dependencies")
				def allDeps = configurations.mavenDeps.allDependencies +
					configurations.serverLib.allDependencies + 
					configurations.shade.allDependencies
				allDeps.each {
					def dependencyNode = dependenciesNode.appendNode("dependency")
					dependencyNode.appendNode("groupId", it.group)
					dependencyNode.appendNode("artifactId", it.name)
					dependencyNode.appendNode("version", it.version)
					//if (it.group == "com.sk89q.worldedit") // Hack to get classifier for worldedit attached (bug https://issues.gradle.org/browse/GRADLE-3030)
						//dependencyNode.appendNode("classifier", "dev")
				}
			}
		}
	}

	repositories {
		if (project.hasProperty("forgemaven_user") && project.hasProperty("forgemaven_password")) {
			maven {
				credentials {
					username project.forgemaven_user
					password project.forgemaven_password
				}
				url "https://files.minecraftforge.net/maven/manage/upload"
			}
		} else {
			maven { url "file://${project.projectDir}/build/repo" }
		}
	}
}

task publishAndClean(type: Delete, dependsOn: "publish") {
    delete javadocJar
}
